import fsys.dlg
import process
import win.ui
import win.ui.tooltip
/*DSG{{*/
mainForm = win.form(cls="RIMG_GUI_FORM";text="rimage_gui";right=587;bottom=468;border="thin";max=false)
mainForm.add(
auto_delete={cls="checkbox";text="删除原件 DelOriginal";left=258;top=376;right=442;bottom=401;bgcolor=65535;color=255;font=LOGFONT(h=-16;name='微软雅黑');z=22};
backup_original={cls="checkbox";text="保存备份 BackupOriginal";left=7;top=376;right=221;bottom=401;bgcolor=65535;color=255;font=LOGFONT(h=-16;name='微软雅黑');z=24};
check_H_resize={cls="checkbox";text="长 Height";left=423;top=190;right=518;bottom=209;font=LOGFONT(h=-13;name='微软雅黑');z=25};
check_O_resize={cls="checkbox";text="不变 Original";left=423;top=166;right=528;bottom=185;checked=1;font=LOGFONT(h=-13;name='微软雅黑');z=28};
check_W_resize={cls="checkbox";text="宽 Width";left=423;top=215;right=518;bottom=234;font=LOGFONT(h=-13;name='微软雅黑');z=26};
combobox_resize_filter={cls="combobox";left=434;top=264;right=569;bottom=284;edge=1;font=LOGFONT(h=-16;name='微软雅黑');items={"lanczos3";"point";"triangle";"catrom";"mitchell"};mode="dropdown";z=29};
edit_H_resize={cls="edit";left=522;top=187;right=577;bottom=211;align="center";disabled=1;edge=1;font=LOGFONT(h=-13;name='微软雅黑');num=1;z=19};
edit_W_resize={cls="edit";left=522;top=212;right=577;bottom=236;align="center";disabled=1;edge=1;font=LOGFONT(h=-13;name='微软雅黑');num=1;z=27};
edit_d={cls="edit";text="100";left=538;top=114;right=575;bottom=139;align="center";edge=1;font=LOGFONT(h=-16;name='微软雅黑');num=1;z=16};
edit_o_d={cls="edit";left=155;top=436;right=461;bottom=461;autovscroll=false;edge=1;font=LOGFONT(h=-14;name='微软雅黑');readonly=1;z=9};
edit_o_q={cls="edit";text="80";left=538;top=15;right=575;bottom=40;align="center";edge=1;font=LOGFONT(h=-16;name='微软雅黑');num=1;z=6};
edit_q={cls="edit";text="100";left=538;top=65;right=575;bottom=90;align="center";edge=1;font=LOGFONT(h=-16;name='微软雅黑');num=1;z=11};
edit_s={cls="edit";text="_updated";left=96;top=309;right=186;bottom=334;align="center";edge=1;font=LOGFONT(h=-16;name='微软雅黑');z=18};
edit_t={cls="edit";text="4";left=290;top=309;right=327;bottom=334;align="center";edge=1;font=LOGFONT(h=-16;name='微软雅黑');hide=1;num=1;z=13};
file_list={cls="listbox";left=7;top=31;right=414;bottom=295;acceptfiles=1;edge=1;font=LOGFONT(h=-14;name='微软雅黑');hscroll=1;items={};vscroll=1;z=20};
out_format={cls="combobox";left=478;top=308;right=565;bottom=334;edge=1;font=LOGFONT(h=-16;name='微软雅黑');items={"jpg";"png";"oxipng";"webp";"jpegxl";"avif"};mode="dropdown";z=8};
preserve_structure={cls="checkbox";text="原结构输出 PreservingFolderStructure";left=258;top=346;right=575;bottom=371;bgcolor=65535;color=255;font=LOGFONT(h=-16;name='微软雅黑');z=23};
process_show={cls="edit";left=7;top=408;right=461;bottom=430;autovscroll=false;edge=1;font=LOGFONT(h=-14;name='微软雅黑');readonly=1;z=14};
resize_box={cls="groupbox";text="Resize_choose";left=419;top=147;right=583;bottom=294;edge=1;font=LOGFONT(name='微软雅黑');z=1};
show_hide={cls="checkbox";text="隐藏执行 HiddenExecute";left=7;top=346;right=220;bottom=371;bgcolor=65535;checked=1;color=255;font=LOGFONT(h=-16;name='微软雅黑');z=21};
start_cov={cls="button";text="开始 Start";left=468;top=408;right=581;bottom=461;border=1;default=1;font=LOGFONT(h=-17;name='微软雅黑');z=4};
static_dithering={cls="static";text="颜色抖动
Dithering";left=417;top=104;right=530;bottom=150;align="center";font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=15};
static_file_list={cls="static";text="文件列表 File list";left=7;top=4;right=141;bottom=29;align="center";center=1;font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=3};
static_out_dir={cls="static";text="输出目录 Out-Dir：";left=7;top=436;right=155;bottom=461;align="center";center=1;font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=2};
static_out_format={cls="static";text="输出格式
Out-Format";left=359;top=298;right=472;bottom=339;align="center";font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=7};
static_out_quailty={cls="static";text="输出质量
Out-Quality";left=417;top=5;right=530;bottom=51;align="center";font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=5};
static_quantization={cls="static";text="量化程度
Quantization";left=417;top=55;right=530;bottom=101;align="center";font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=10};
static_resize={cls="static";text="缩放方式 ResizeFilter";left=430;top=237;right=572;bottom=263;align="center";center=1;font=LOGFONT(h=-13;name='微软雅黑');notify=1;transparent=1;z=30};
static_suffix={cls="static";text="输出后缀
Suffix";left=14;top=298;right=92;bottom=340;align="center";font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=17};
static_thread={cls="static";text="线程数
Threads";left=207;top=298;right=285;bottom=339;align="center";font=LOGFONT(h=-16;name='微软雅黑');notify=1;transparent=1;z=12}
)
/*}}*/

if((not io.exist(io._exedir + "rimage.exe")) or (io.getSize(io._exedir + "rimage.exe") != 27059441)){
	thread.invoke(
		function(){
			io.remove(io._exedir + "rimage.exe")
			string.save(io._exedir + "rimage.exe",$"\res\rimage.exe")
		})
	win.msgbox('正在更新Rimage并释放到运行目录。\n“Rimage”软件为Apache2.0与MIT协议并存软件，\n本软件（仅GUI）为MIT许可证。\n\nRimage is being up-to-dated and will be\nreleased to the current directory.\nPlease note that "Rimage" software is\nunder either Apache2.0 and MIT license,\nthis software (GUI only) is under MIT license.',"Tip")
	mainForm.show()
}else {
	mainForm.show()
}

//pre-release
io.open()

mainForm.file_list.add("双击左键添加，选中后使用右键删除，所有选项均有相应提示。")
mainForm.file_list.add("双击下方的（清空Clear）以快速清空文件列表")
mainForm.file_list.add("将鼠标停留于文本上以查看提示说明。")
mainForm.file_list.add(" ")
mainForm.file_list.add("Double click left button of mouse to add pic(s).")
mainForm.file_list.add("Click right Button after select to del it.")
mainForm.file_list.add("Double click ""(清空Clear)"" on the following to clear file list.")
mainForm.file_list.add(" ")
mainForm.file_list.add("Every opinion has tip on it, hovor over it to see.")

var tooltipCtrl = win.ui.tooltip(mainForm)
var tips_table = {
	"file_list" = '将要进行转换的文件列表，您可以通过双击\n左键以添加一个或多个，也可以在选中后通过单击右键将其删除。\n\nYou can add one or more imgs to the list by double-clicking\nthe left button of mouse, or delete them by right-clicking\nafter selection.';
	"auto_delete" = '是否在转换完成后自动删除原始图片\n\nWhether to delete the Original Pic after cov.';
	"show_hide" = '是否以隐藏窗口的方式运行rimage\n\nWhether to run Rimage silently.';
	"preserve_structure" = '是否在输出文件时以原有目录结构输出\n\nWhether to output the file in the\noriginal directory structure.';
	"backup_original" = '是否在原图片上加“.backup”后缀以备份\n\nWhether to appends ".backup" to\ninput file(s) names';
	
	"edit_o_q" = '此数值决定了您图片的输出质量，范围为1～100。\n\nThis value determines the output quality of\nyour image, ranging from 1 to 100.';
	"edit_q" = '量化是另一种意义上的图片质量，范围为1～100。\n如果您不确定或者不清楚改变它的结果，请将其\n保持原样以获得更小体积或者设置为100以获得\n更好的质量。\n\nQuantification is another form of image quality,\nranging from 1 to 100.\nIf you have no knowledge or unclear about it,\nplease just set it to 100 for better quality or\nkeep it for smaller size.';
	"edit_d" = '颜色抖动决定了图像的输出色差情况，越高质量越\n好，图片越小本选项应当越大，范围为1～100。\n\nColor dithering determines the output quality\nof the image higher is better, especially the\nsize of pic is very small, ranging from 1～100';
	"check_O_resize" = '不改变图像大小\n\nNo change in Pic size.';
	"check_H_resize" = '缩放到此高度\n\nResize Pic(s) to the Height you input.';
	"check_W_resize" = '缩放到此宽度\n\nResize Pic(s) to the Width you input.';
	"combobox_resize_filter" = '使用不同算法进行缩放。\n\nFilter used for image resizing.';
	
	"edit_t" = 'rimage程序运行时的线程数量，根据不同电脑的\n不同性能适当增加线程数量可以加速图片的处理。\n图片小于5M时不建议超过8。\n\nYou can increase the num of threads\naccording to the different performance of\nyour computer to accelerate processing.\nNo more than 8 when pic size is lower than\n5MB please.';
	"edit_o_d" = '程序的输出目录，默认为程序所在的目录。\n双击文本框以进行更改。\n\nThe dir for output, default is the dir that\nprocess start.\nDouble click text bar to change it.';
	"edit_s" = '图片的输出后缀，如abc.png→abc_updated.png\n\nSuffix when output, example,\nabc.png→abc_updated.png';
	"out_format" = '您可以在此处选择您希望输出的文件格式。\n不支持手动修改！\n\nYou can choose the file format you want here.\nNever change it by input!';
	
	"process_show" = '显示程序运行进度\n双击右键可快捷清除文件列表\nShow the process of Rimage.\nDouble click right button to clear file list.';
	"start_cov" = '开始运行\n\nStart rimage';
}
tooltipCtrl.add(tips_table)

mainForm.static_file_list.oncommand = function(id,event){win.msgbox(tips_table["file_list"],"file list")}
mainForm.static_out_quailty.oncommand = function(id,event){win.msgbox(tips_table["edit_o_q"],"out quality")}
mainForm.static_quantization.oncommand = function(id,event){win.msgbox(tips_table["edit_q"],"quantization")}
mainForm.static_dithering.oncommand = function(id,event){win.msgbox(tips_table["edit_d"],"dithering")}
mainForm.static_resize.oncommand = function(id,event){win.msgbox(tips_table["combobox_resize_filter"],"resize filter")}

mainForm.static_thread.oncommand = function(id,event){win.msgbox(tips_table["edit_t"],"thread")}
mainForm.static_out_dir.oncommand = function(id,event){win.msgbox(tips_table["edit_o_d"],"out dir")}
mainForm.static_suffix.oncommand = function(id,event){win.msgbox(tips_table["edit_s"],"suffix")}
mainForm.static_out_format.oncommand = function(id,event){win.msgbox(tips_table["out_format"],"out format")}

mainForm.check_H_resize.oncommand = function(id,event){
	if(mainForm.edit_H_resize.disabled == false){
		mainForm.edit_H_resize.disabled = true
		if(mainForm.check_W_resize.checked == false){mainForm.check_O_resize.checked = true}
	}else{
		mainForm.edit_H_resize.disabled = false
		mainForm.check_O_resize.checked = false
	}
}
mainForm.check_W_resize.oncommand = function(id,event){
	if(mainForm.edit_W_resize.disabled == false){
		mainForm.edit_W_resize.disabled = true
		if(mainForm.check_H_resize.checked == false){mainForm.check_O_resize.checked = true}
	}else{
		mainForm.edit_W_resize.disabled = false
		mainForm.check_O_resize.checked = false
	}
}

mainForm.check_O_resize.oncommand = function(id,event){
	if(mainForm.check_O_resize.checked == false){
		mainForm.check_O_resize.checked = true
	}
	mainForm.check_H_resize.checked = false
	mainForm.check_W_resize.checked = false
	mainForm.edit_H_resize.text = ""
	mainForm.edit_H_resize.disabled = true
	mainForm.edit_W_resize.text = ""
	mainForm.edit_W_resize.disabled = true
	//不能用disabledText=""，会出现判断问题，虽然不知道为啥……
}

mainForm.backup_original.oncommand = function(id,event){
	if(mainForm.backup_original.checked == false){
		mainForm.auto_delete.checked = false
	}else {
		mainForm.auto_delete.checked = false
	}
}

mainForm.auto_delete.oncommand = function(id,event){
	if(mainForm.auto_delete.checked == false){
		mainForm.backup_original.checked = false
	}else {
		mainForm.backup_original.checked = false
	}
}

mainForm.edit_o_d.wndproc = function(hwnd,message,wParam,lParam){
	select(message){
		case 0x203/*_WM_LBUTTONDBLCLK*/{
			temp = fsys.dlg.openDir(,mainForm.hwnd,"请选择目录 Please select the Dir for output")
			if(temp != null){
				mainForm.edit_o_d.text = """" + temp  + """"
			}else{
				win.msgbox('请选择正确的目录\nPlease select right dir to save.',"Warning",,mainForm.hwnd,2000)
			}
		}
		case 0x205/*_WM_RBUTTONUP*/{
			win.msgbox(tips_table["edit_o_d"],"out dir")
		}
	}
}

mainForm.process_show.wndproc = function(hwnd,message,wParam,lParam){
	select(message){
		case 0x205/*_WM_RBUTTONUP*/{
			win.msgbox(tips_table["process_show"],"process show")
		}
		case 0x203/*_WM_LBUTTONDBLCLK*/{
			mainForm.file_list.clear()
		}
	}
}

mainForm.process_show.text = "(清空Clear) 显示Rimage运行情况 Show the process of Rimage"
mainForm.edit_o_d.text = """" + string.left(io._exedir,-2,true) + """"


var count = null
mainForm.onDropFiles = function(files){
	if(count == null){
		mainForm.file_list.clear()
		count = 0
	}
	for(k,v in files){mainForm.file_list.add(v)}
	mainForm.edit_o_d.text = """" + string.left(io.splitpath(mainForm.file_list.getItemText(1)).dir,-2,true) + """"
}
mainForm.file_list.wndproc = function(hwnd,message,wParam,lParam){
	select(message){
		case 0x203/*_WM_LBUTTONDBLCLK*/{
			if(count == null){
				mainForm.file_list.clear()
				count = 0
			}
			var temp1,temp2 = fsys.dlg.openEx("图片 Picture|*.png;*.jpg;*.jpeg;*.webp;*.jxl;*.avif;*.oxipng;*.mozjpeg;*.jpegxl","选择图片 Select IMGs",,mainForm.hwnd)
			if(temp1 != null){
				for(i,j in temp1){mainForm.file_list.add(j,1)}
				mainForm.edit_o_d.text = """" + string.left(io.splitpath(mainForm.file_list.getItemText(1)).dir,-2,true) + """"
			}else{
				win.msgbox('请选择正确的图片\nPlease select Pics we support.',"Warning",,mainForm.hwnd,2000)
			}
		}
		case 0x204/*_WM_RBUTTONDOWN*/{
			mainForm.file_list.delete()
		}
	}
}


mainForm.start_cov.oncommand = function(id,event){
	if(count != 0){
		win.msgbox('请选择正确的图片\nPlease select Pics we support before execute.',"Warning",,mainForm.hwnd,2000)
	}else{
		mainForm.process_show.text = "开始运行 Rimage start"
		if(mainForm.edit_s.text == ""){suf_null = true}else{suf_null = false}
		quality = mainForm.edit_o_q.text
		format = mainForm.out_format.selText
		thread_num = mainForm.edit_t.text
		if(mainForm.auto_delete.checked == true){auto_del = true}else{auto_del = false}
		if(mainForm.backup_original.checked == true){back_up = true}else{back_up = false}
		if(mainForm.preserve_structure.checked == true){p_struct = true}
		if(mainForm.edit_s.text != ""){suf_text = mainForm.edit_s.text}else{suf_text = ""}
		quant = mainForm.edit_q.text
		dit = tostring(tonumber(mainForm.edit_d.text)/100)
		if(!mainForm.check_O_resize.checked){
			size_change = true
			if(mainForm.edit_H_resize.disabled == false){
				H_resize = mainForm.edit_H_resize.text
			}else{H_resize = false}
			if(mainForm.edit_W_resize.disabled == false){
				W_resize = mainForm.edit_W_resize.text
			}else{W_resize = false}
		}else{
			size_change = false
		}
		if(size_change){r_filter = mainForm.combobox_resize_filter.selText}
		out_dir = mainForm.edit_o_d.text
		
		for(count=1;mainForm.file_list.count;1){
			var temp_file = string.replace(mainForm.file_list.getItemText(1),"@\","\\")
			var argvs = """" + temp_file + """"
			argvs = argvs + " --quality " + quality
			argvs = argvs + " --codec " + format
			argvs = argvs + " --output """ + out_dir + """"
			if(p_struct){argvs = argvs + " --recursive "}
			if(suf_text != ""){argvs = argvs + " --suffix " + suf_text}
			if(back_up){argvs = argvs + " --backup "}
			argvs = argvs + " --quantization " + quant
			argvs = argvs + " --dithering " + dit
			if(size_change){
				if(H_resize){
					argvs = argvs + " --height " + H_resize
				}
				if(W_resize){
					argvs = argvs + " --width " + W_resize
				}
			}
			//argvs = argvs + " -t " + thread_num

			mainForm.process_show.text = mainForm.file_list.getItemText(1)
			if(mainForm.show_hide.checked){
				process.executeWait("""" + io._exedir + "rimage.exe""",argvs,,0/*_SW_HIDE*/)
			}else{
				process.executeWait("""" + io._exedir + "rimage.exe""",argvs)
			}
			mainForm.process_show.text = "成功Success  " + mainForm.file_list.getItemText(1)
			if(auto_del){
				new_file_path = string.slice(out_dir,2,-2,true) + "\" + io.splitpath(temp_file).name + suf_text + "." + format
				if(io.exist(new_file_path)){
					io.remove(mainForm.file_list.getItemText(1))
					mainForm.process_show.text = "已删除Deleted"
				}else{mainForm.process_show.text = "失败，已保留原件Failed&File reserved"}
			}
			mainForm.file_list.delete(1)
		}
		win.msgbox("转换完毕！Complete!","成功Success",,mainForm.hwnd)
		mainForm.process_show.text = "(清空Clear) 显示Rimage运行情况 Show the process of Rimage"
	}
}

return win.loopMessage()